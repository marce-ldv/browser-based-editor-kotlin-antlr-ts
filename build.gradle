buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // antlr("org.antlr:antlr4:4.5")
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'com.example'
version '0.0.1'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    runtime 'org.antlr:antlr4:4.7.2'
}

task generateLexer(type:JavaExec) {
    def lexerName = "CalcLexer"
    inputs.file("$ANTLR_SRC/${lexerName}.g4")
    outputs.file("$GEN_JS_SRC/${lexerName}.js")
    outputs.file("$GEN_JS_SRC/${lexerName}.interp")
    outputs.file("$GEN_JS_SRC/${lexerName}.tokens")
    main = 'org.antlr.v4.Tool'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-Dlanguage=JavaScript', "${lexerName}.g4", '-o', '../../main-generated/javascript']
    workingDir = ANTLR_SRC
}
task generateParser(type:JavaExec) {
    dependsOn generateLexer
    def lexerName = "CalcLexer"
    def parserName = "CalcParser"
    inputs.file("$ANTLR_SRC/${parserName}.g4")
    inputs.file("$GEN_JS_SRC/${lexerName}.tokens")
    outputs.file("$GEN_JS_SRC/${parserName}.js")
    outputs.file("$GEN_JS_SRC/${parserName}.interp")
    outputs.file("$GEN_JS_SRC/${parserName}.tokens")
    main = 'org.antlr.v4.Tool'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-Dlanguage=JavaScript', "${parserName}.g4", '-no-listener', '-no-visitor', '-o', '../../main-generated/javascript']
    workingDir = ANTLR_SRC
}
